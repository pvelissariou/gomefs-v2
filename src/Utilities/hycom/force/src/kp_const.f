      PROGRAM PARINT
      USE MOD_ZA  ! HYCOM array I/O interface
      IMPLICIT NONE
C
      INTEGER    NREC
      PARAMETER (NREC=12)
C
C     FLUX ARRAYS.
C
      INTEGER, ALLOCATABLE :: MSK(:,:)
      REAL*4,  ALLOCATABLE :: PLON(:,:),PLAT(:,:),XAF(:,:),YAF(:,:)
      REAL*4,  ALLOCATABLE :: KPM(:,:)
C
      CHARACTER PREAMBL(5)*79
C
C     NAMELIST.
C
      INTEGER          JPR
      COMMON/NPROCS/   JPR
      SAVE  /NPROCS/
C
      CHARACTER*40     CTITLE
      NAMELIST/AFTITL/ CTITLE
      REAL*4           KPAR(NREC)
      NAMELIST/AFCNST/ KPAR
C
C**********
C*
C 1)  FROM A SEQUENCE OF MONTHLY kPAR VALUES, CREATE A
C      CONSTANT FIELD MODEL GRID KPAR FILE SUITABLE FOR INPUT
C      TO THE HYCOM OCEAN MODEL OVER THE GIVEN REGION.
C
C 2)  PARAMETERS:
C
C     MODEL GRID SPECIFICATION (W.R.T. PRESSURE GRID):
C
C        IDM    = 1ST DIMENSION OF MAJOR (HYCOM) MODEL ARRAYS
C        JDM    = 2ND DIMENSION OF MAJOR (HYCOM) MODEL ARRAYS
C
C 3)  NAMELIST INPUT:
C
C     /AFTITL/
C        CTITLE - ONE (40-CHARACTER) LINE TITLE.
C
C     /AFCNST/
C        KPAR   - MONTHY kPAR (1/m)
C
C 4)  THE OUTPUT FIELDS HAVE THEIR COMPONENTS ON EVERY GRID POINT OF 
C      THE MODEL'S 'P' GRID.  ARRAY SIZE IS 'IDM' BY 'JDM, AND THE DATA
C      IS OUTPUT .a/.b FORMAT TOGETHER WITH MONTH.
C*
C**********
C
      INTEGER I,J,KREC
      REAL*4  XMIN,XMAX
C
C --- MODEL ARRAYS.
C
      CALL XCSPMD  !define idm,jdm
      ALLOCATE(  MSK(IDM,JDM) )
      ALLOCATE(  KPM(IDM,JDM) )
C
C     NAMELIST INPUT.
C
      CALL ZHOPEN(6, 'FORMATTED', 'UNKNOWN', 0)
C
      CTITLE = ' '
      WRITE(6,*) 'READING /AFTITL/'
      CALL ZHFLSH(6)
      READ( 5,AFTITL)
      WRITE(6,AFTITL)
C
      KPAR = 0.0
      WRITE(6,*) 'READING /AFCNST/'
      CALL ZHFLSH(6)
      READ( 5,AFCNST)
      WRITE(6,AFCNST)
      WRITE(6,*) 
      CALL ZHFLSH(6)
C
C     INITIALIZE HYCOM OUTPUT.
C
      CALL ZAIOST
      CALL ZAIOPN('NEW', 10)
      CALL ZHOPEN(10, 'FORMATTED', 'NEW', 0)
C
      PREAMBL(1) = CTITLE
      PREAMBL(2) = ' '
      PREAMBL(3) = ' '
      PREAMBL(4) = ' '
      WRITE(PREAMBL(5),'(A,2I5)')
     +        'i/jdm =',
     +       IDM,JDM
      WRITE(10,4101) PREAMBL
      WRITE(6,*)
      WRITE(6, 4101) PREAMBL
      WRITE(6,*)
C
C     PROCESS ALL THE KPAR RECORDS.
C
      DO 810 KREC= 1,NREC
C
C       CONSTANT FIELDS.
C
        DO J= 1,JDM
          DO I= 1,IDM
            KPM(I,J) = KPAR(KREC)
          ENDDO
        ENDDO
C
C       WRITE OUT HYCOM KPAR.
C
        CALL ZAIOWR(KPM,MSK,.FALSE., XMIN,XMAX, 10, .FALSE.)
        WRITE(10,4102) '    kpar',KREC,XMIN,XMAX
        WRITE( 6,4102) '    kpar',KREC,XMIN,XMAX
        CALL ZHFLSH(10)
  810 CONTINUE
C
      CALL ZAIOCL(10)
      CLOSE( UNIT=10)
      STOP
C
 4101 FORMAT(A79)
 4102 FORMAT(A,': month,range = ',I2.2,1P2E16.7)
C     END OF PROGRAM FLXINT.
      END
